import groovy.json.JsonSlurper
import org.apache.tools.ant.taskdefs.condition.Os

/*
 * Copyright (c) 2024, WSO2 LLC. (http://www.wso2.org) All Rights Reserved.
 *
 * WSO2 LLC. licenses this file to you under the Apache License,
 * Version 2.0 (the "License"); you may not use this file except
 * in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */
description = 'Ballerina - SAP S/4HANA Sales API'

subprojects {
    ext {
        packageOrg = "ballerinax"
        ballerinaTomlFilePlaceHolder = new File("${project.rootDir}/build-config/resources/Ballerina.toml")
    }

    task updateDocumentationFiles
}

def moduleMDPlaceholder = new File("${project.rootDir}/build-config/resources/Module.md")
def packageMDPlaceholder = new File("${project.rootDir}/build-config/resources/Package.md")

task updateDocumentationFiles {
    doLast {
        def files = []
        new File("${project.rootDir}/ballerina").eachDir { files << it.name }

        for (String dir in files) {

            if (dir == "resources") {
                continue
            }

            def packageProperties = new File("${project.rootDir}/ballerina/${dir}/docs.json")
            def parsedJson = new JsonSlurper().parseText(packageProperties.text)

            def moduleMDFile = new File("${project.rootDir}/ballerina/${dir}/Module.md")

            def newModuleMD = moduleMDPlaceholder.text
            newModuleMD = newModuleMD.replace("@package-name@", "sap.s4hana.${dir}")
            newModuleMD = newModuleMD.replace("@description@", parsedJson.description)
            newModuleMD = newModuleMD.replace("@communication-scenario@", parsedJson."communication-scenario")
            newModuleMD = newModuleMD.replace("@import-statement@", parsedJson."import-statement")
            newModuleMD = newModuleMD.replace("@client-init@", parsedJson."client-init")
            newModuleMD = newModuleMD.replace("@api-invocation@", parsedJson."api-invocation")
            moduleMDFile.text = newModuleMD

            def packageMDFile = new File("${project.rootDir}/ballerina/${dir}/Package.md")

            def newPackageMD = packageMDPlaceholder.text
            newPackageMD = newPackageMD.replace("@package-name@", "sap.s4hana.${dir}")
            newPackageMD = newPackageMD.replace("@description@", parsedJson.description)
            newPackageMD = newPackageMD.replace("@communication-scenario@", parsedJson."communication-scenario")
            newPackageMD = newPackageMD.replace("@import-statement@", parsedJson."import-statement")
            newPackageMD = newPackageMD.replace("@client-init@", parsedJson."client-init")
            newPackageMD = newPackageMD.replace("@api-invocation@", parsedJson."api-invocation")
            packageMDFile.text = newPackageMD

        }

        project.exec {
            ignoreExitValue true
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "git commit -m \"[Automated] Update the documentation files\" */Module.md */Package.md"
            } else {
                commandLine 'sh', '-c', "git commit -m '[Automated] Update the documentation files' */Module.md */Package.md"
            }
        }
    }
}

task build {
    dependsOn(updateDocumentationFiles)
    dependsOn(':sales-ballerina:api_salesdistrict_srv:build')
    dependsOn(':sales-ballerina:api_salesorganization_srv:build')
    dependsOn(':sales-ballerina:api_sales_order_srv:build')
    dependsOn(':sales-ballerina:api_sd_sa_soldtopartydetn:build')
    dependsOn(':sales-ballerina:salesarea_0001:build')
    dependsOn(':sales-ballerina:api_sd_incoterms_srv:build')
    dependsOn(':sales-ballerina:api_sales_inquiry_srv:build')
    dependsOn(':sales-ballerina:api_sales_quotation_srv:build')
    dependsOn(':sales-ballerina:api_sales_order_simulation_srv:build')
}

publishToMavenLocal.dependsOn build
publish.dependsOn build
